
buildscript {
    ext {
        junit5Version = '5.13.1' // https://junit.org/junit5/

        slf4jVersion = '2.0.17' // http://www.slf4j.org/download.html
        log4jVersion = '2.24.3' // https://logging.apache.org/log4j/2.x/release-notes.html
        // Note: Since Jackson 2.14, Android SDK 26 is required
        // https://github.com/FasterXML/jackson/wiki/Jackson-Releases
        jacksonVersion = '2.19.1' // https://github.com/FasterXML/jackson-databind/tags
        retrofitVersion = "3.0.0" // https://github.com/square/retrofit/tags
        shadowVersion = '8.3.6' // https://github.com/GradleUp/shadow/releases
        ioLibVersion = '2.3.0' // https://github.com/retrodaredevil/io-lib/releases // TODO update this to update jSerialComm version
        okhttpVersion = "4.12.0" // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
        couchdbJavaVersion = '1.1.2'
//        couchdbJavaVersion = "8582268195".substring(0, 10)
        dockerComposePluginVersion = "0.17.12" // https://github.com/avast/gradle-docker-compose-plugin/releases
        errorpronePluginVersion = '4.2.0' // https://github.com/tbroyer/gradle-errorprone-plugin/releases
        errorproneCoreVersion = '2.38.0' // https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
        antlrVersion = "4.13.2" // https://github.com/antlr/antlr4/releases
    }
}


plugins {
    id 'com.diffplug.spotless' version '7.0.4' // https://mvnrepository.com/artifact/com.diffplug.spotless/spotless-plugin-gradle
    id 'net.ltgt.errorprone' version "$errorpronePluginVersion"
    id "com.avast.gradle.docker-compose" version "$dockerComposePluginVersion"
}

version "0.0.1-SNAPSHOT"
description = """SolarThing"""
group = "me.retrodaredevil.solarthing"


spotless {
    // More info on ratchet https://github.com/diffplug/spotless/tree/master/plugin-gradle#ratchet
//    ratchetFrom 'origin/master'
    // https://github.com/diffplug/spotless/tree/master/plugin-gradle#eclipse-jdt if you have eclipse xml
    enforceCheck = false // doesn't have to be formatted correctly for ./gradlew build to work
    format 'javaBasic', com.diffplug.gradle.spotless.JavaExtension, {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        removeUnusedImports()
        endWithNewline()
        trimTrailingWhitespace()
    }
    format 'javaIndent', com.diffplug.gradle.spotless.JavaExtension, {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        leadingSpacesToTabs()
//        toggleOffOn()
//        toggleOffOn("/*", "*/") // It doesn't like tabs then a space, which are present in Javadocs, so just disable
        // NOTE: Only one toggleOffOn is supported
        toggleOffOn("\"\"\"", "\"\"\"")
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }

    // https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
    tasks.withType(AbstractArchiveTask).configureEach {
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'net.ltgt.errorprone'
    repositories {
        maven { url "https://jitpack.io" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }
    dependencies {
        // https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
        errorprone "com.google.errorprone:error_prone_core:$errorproneCoreVersion"
        errorprone "com.google.errorprone:error_prone_annotations:$errorproneCoreVersion"

        // https://junit.org/junit5/docs/current/user-guide/#running-tests-build-gradle
        testImplementation(platform("org.junit:junit-bom:$junit5Version"))
        testImplementation("org.junit.jupiter:junit-jupiter")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")

        testImplementation 'com.tngtech.archunit:archunit:1.3.2' // https://github.com/TNG/ArchUnit/releases

        // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
        implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
        implementation 'org.jetbrains:annotations:26.0.2' // https://github.com/JetBrains/java-annotations/releases
    }
    test {
        useJUnitPlatform()
    }
    tasks.withType(JavaCompile).configureEach {
        options.errorprone {
//            disableWarningsInGeneratedCode = true // doesn't seem to work
            disable("MissingSummary") // Our JavaDocs don't need to be perfect
            disable("StringSplitter") // Yes String.split(String) has surprising behavior, but we know how to be careful.
            disable("EqualsGetClass") // Most of the time we prefer that equality checks don't evaluate to true for subclasses
            disable("NullableOnContainingClass") // This is dumb, who would do it this way? A.B reads as a fully qualified name to me...
            disable("InvalidLink") // So many times this is wrong. #equals(Object) is a valid link!
            disable("ComparableType") // different Identifiers need to be able to be compared to each other, so we disable this for now.
            disable("DoNotClaimAnnotations") // TODO figure how how to make our annotation processor more "correct"
//            disable("ReturnValueIgnored") // remove this once this is fixed: https://github.com/google/error-prone/issues/3292 // TODO can we enable this now?
            disable("UnusedMethod") // This is useful, but has this issue: https://github.com/google/error-prone/issues/3144 // issue is now "completed", but annotations not customizable for
            disable("InlineMeSuggester") // Wrong when annotated by annotations, and I don't need this feature
            disable("EmptyCatch") // This is a great warning, but IntelliJ wants ignored exceptions to be named "ignored" and ErrorProne wants "ok"...
            disable("FutureReturnValueIgnored") // For "send and forget" stuff, this is really annoying
            disable("UnusedVariable") // We are OK with this most of the time
            disable("CanonicalDuration") // I will specify my durations in whatever units I please
            disable("AssignmentExpression") // TODO stop doing requireNonNull(this.asdf = asdf);
            disable("PatternMatchingInstanceof") // TODO reenable and use pattern matching instanceof


            error("MissingOverride")

            // Experimental Errors
            enable("ClassName")
            warn("DepAnn")
            warn("EmptyIf")
            warn("InsecureCryptoUsage")
            // Experimental Warnings
            warn("AnnotationPosition")
            warn("ConstantPatternCompile")
            warn("DifferentNameButSame")
            error("EqualsBrokenForNull")
            warn("InconsistentOverloads")
            warn("InitializeInline")
            warn("InterfaceWithOnlyStatics")
            warn("InterruptedExceptionSwallowed")
            warn("Interruption")
//            warn("MemberName") // useful, but stuff like _myVariable does not work, and that can be useful *sometimes*
            warn("NonCanonicalStaticMemberImport")
//            warn("PreferJavaTimeOverload") // SolarThing uses a lot of dateMillis, and many times it does not make sense to use java.time
            warn("RedundantOverride")
            warn("RedundantThrows")
//            warn("ThrowSpecificExceptions") // TODO Something we need to work on in the SolarThing codebase
            warn("TransientMisuse")
            warn("TryWithResourcesVariable")
            warn("UnnecessarilyFullyQualified")
            warn("UnnecessaryAnonymousClass") // suggest
            warn("UnusedException")

            // Experimental Suggestions
            warn("ConstantField")
//            warn("FieldCanBeFinal") // Useful on everything but :client module, because of the usages of @JsonProperty on mutable fields.
            warn("FieldCanBeLocal")
            warn("FieldCanBeStatic")
//            warn("FieldMissingNullable") // There are many private fields where this is easy enough to tell // TODO enable this at some point (yay nullability annotations)
            warn("ForEachIterable")
            warn("LambdaFunctionalInterface")
//            warn("MethodCanBeStatic") // TODO Very, very useful, but I don't need this warning in tests
//            error("MissingBraces") // I would love to enforce this in all places except equals(), so disable for now
            error("MixedArrayDimensions")
//            error("MultiVariableDeclaration") // While frowned upon many times, we use this a lot and it is helpful somtimes
            error("MultipleTopLevelClasses")
            error("PackageLocation")
//            warn("ParameterMissingNullable") // This is useful. Maybe uncomment if we become more religious about use of @Nullable
            error("PrivateConstructorForUtilityClass") // not completely ideal for spring classes with static initialization
            warn("RemoveUnusedImports") // Only a warning because https://github.com/antlr/antlr4/issues/2568 and https://github.com/google/error-prone/issues/463
//            warn("ReturnMissingNullable") // Useful onlyl if we become more religious about use of @Nullable
            warn("ReturnsNullCollection")
            warn("SwitchDefault")
            error("SymbolToString")
            warn("ThrowsUncheckedException") // we should be unchecked exceptions in JavaDoc instead
            warn("TryFailRefactoring")
            error("TypeToString")
//            warn("UngroupedOverloads") //
            warn("UnnecessaryBoxedAssignment")
            warn("UnnecessaryBoxedVariable")
            error("UnnecessaryStaticImport")

        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile){
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                // thanks https://stackoverflow.com/a/68515859/5434860
                groupId = "${group}"
                artifactId = "${project.name}"
                version = "${version}"

                from project.components.java
            }
        }
    }
}
project(":core"){
    apply plugin: 'java'
    dependencies {
        annotationProcessor project(":process-annotations")
    }
}
project(":common"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
        api project(":action-lang")
        annotationProcessor project(":process-annotations")
    }
}
project(":client"){
    apply plugin: 'java'
    apply plugin: 'docker-compose'
    dependencies {
        api project(":core")
        api project(":common")
        api project(":serviceapi")
        annotationProcessor project(":process-annotations")
    }
    test {
        useJUnitPlatform {
            excludeTags 'integration'
        }
    }
    tasks.register('integration', Test) {
        // In the future, we can use the Test Suite feature rather than doing this manually: https://docs.gradle.org/7.3.1/userguide/jvm_test_suite_plugin.html
        useJUnitPlatform {
            includeTags 'integration'
        }
    }
    dockerCompose {
        useComposeFiles = ['../testing/couchdb-compose.yml']
        isRequiredBy integration
    }
}
project(":serviceapi"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
        annotationProcessor project(":process-annotations")
    }
}
project(":server"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
        api project(":common")
        api project(":serviceapi")
        annotationProcessor project(":process-annotations")
    }
}
project(":action-node"){
    apply plugin: 'java'
    dependencies {
        annotationProcessor project(":process-annotations")
    }
}
project(":notation-script"){
    apply plugin: 'java'
    dependencies {
        annotationProcessor project(":process-annotations")
    }
}
project(":action-lang"){
    apply plugin: 'java'
    dependencies {
        api(project(":notation-script")){
            // we do this because https://github.com/gradle/gradle/issues/820
            exclude group: "org.antlr", module: "antlr-runtime"
            exclude group: "org.antlr", module: "antlr4"
            // We exclude antlr4-runtime because we add it below as an api dependency rather than an implementation dependency
            exclude group: "org.antlr", module: "antlr4-runtime"
            exclude group: "org.antlr", module: "ST4"
        }
        // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
        api group: 'org.antlr', name: 'antlr4-runtime', version: "$antlrVersion"

        api project(":action-node")
        annotationProcessor project(":process-annotations")
    }
}

wrapper {
    gradleVersion = '8.11.1'
    distributionType = Wrapper.DistributionType.ALL
}
