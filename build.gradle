version "0.0.1-SNAPSHOT"
description = """SolarThing"""

buildscript {
    ext {
        junit5Version = '5.7.0' // https://junit.org/junit5/
        junit5PlatformVersion = '1.7.0'

        slf4jVersion = '1.7.30' // http://www.slf4j.org/download.html
        log4jVersion = '2.14.0' // https://logging.apache.org/log4j/2.x/javadoc.html
        jacksonVersion = '2.12.0' // https://github.com/FasterXML/jackson-databind/releases
        // retrofit is why we need allow-opens. context: https://stackoverflow.com/questions/60915381/retrofit2-maven-project-illegal-reflective-access-warning // https://github.com/square/retrofit/issues/3341
        retrofitVersion = "2.9.0" // https://github.com/square/retrofit/releases
        shadowVersion = '6.1.0' // https://github.com/johnrengelman/shadow/releases
        ioLibVersion = '2.1.0' // https://github.com/retrodaredevil/io-lib/releases
    }
}
allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
        google()
    }
}

subprojects {
    apply plugin: 'java-library'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    repositories {
        maven { url "https://jitpack.io" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }
    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
        testImplementation "org.junit.platform:junit-platform-commons:$junit5PlatformVersion"
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: junit5Version

        // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
        implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
        implementation 'org.jetbrains:annotations:15.0'
    }
    test {
        useJUnitPlatform()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile){
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}
project(":common"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
    }
}
project(":client"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
        api project(":common")
        api project(":serviceapi")
    }
}
project(":serviceapi"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
    }
}
project(":graphql"){
    apply plugin: 'java'
    dependencies {
        api project(":core")
        api project(":common")
        api project(":serviceapi")
    }
}

wrapper {
    gradleVersion = '6.6.1'
    distributionType = Wrapper.DistributionType.ALL
}
